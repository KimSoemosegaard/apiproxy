#!/usr/bin/env perl

##  Pragmas
use strict;
use warnings;
use open qw(:utf8 :std);
use utf8;

##  Packages
use Data::Dumper;
use HTML::PrettyPrinter;
use HTML::TreeBuilder;
use HTTP::Cookies;
use JSON qw(decode_json);
use LWP;
use URI;

##  Browser information
my $useragent = "Mozilla/5.0"
    . " (X11; Ubuntu; Linux x86_64; rv:85.0)"
    . " Gecko/20100101 Firefox/85.0";


##  Main program
{
    # Environment
    $0 =~ s(.*/)()g;

    # URL
    my $url = shift(@ARGV);
    die "Usage: $0 URL\n" unless
	$url && !@ARGV;

    # Browser
    my $lwp = LWP::UserAgent->new;
    $lwp->agent($useragent);
    $lwp->cookie_jar(
	HTTP::Cookies->new(
	    file => "osint-cli.dat",
	    autosave => 1));
	
    # Fetch
    my $res = $lwp->get($url);
    die "ERROR: " . $res->status_line . "\n" unless
	$res->is_success;
    die "ERROR: " . $res->content_type . "\n" unless
	$res->content_type =~ m(html)i;

    # Parse
    my $html = HTML::TreeBuilder->new_from_content($res->decoded_content);
    die "ERROR: HTML::TreeBuilder new_from_content" unless
	$html;

    # Clean
    for my $node ($html->look_down("/" => "/")) { $node->attr("/", undef); }
    for my $node ($html->look_down("_tag" => "link")) { $node->delete; }
    for my $node ($html->look_down("_tag" => "meta")) { $node->delete; }
    for my $node ($html->look_down("_tag" => "noscript")) { $node->delete; }
    for my $node ($html->look_down("_tag" => "script")) { $node->delete; }
    for my $node ($html->look_down("_tag" => "style")) { $node->delete; }

    # Format
    my $hpp = HTML::PrettyPrinter->new(
	allow_forced_nl => 1,
	linelength => 80,
	min_bool_attr => 1,
	quote_attr => 1,
	uppercase => 0);
    $hpp->set_nl_after(1,'br');
    print @{$hpp->format($html)};
}
